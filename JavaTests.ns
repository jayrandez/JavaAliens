Newspeak3
'JavaAliens'
class JavaTests packageUsing: manifest = (|
	private JNILibrary = manifest JNILibrary.
	private JavaAliens = manifest JavaAliensForSqueak.
	private RegexMatcher = manifest RegexMatcher.
	private Generator = manifest Generator.
	List
	Alien
	jni
	java
	regex
	generator
	allTests = {JNILibraryStartVM. JavaAliensCallStaticMethods. JavaAliensRunApplication.}.
|) (
class JNILibraryGenerate classpath: classpath = Test classpath: classpath (|
	CallStatic
|) (
public run = (
	| env |
	java loadClasspath: classpath.
	env:: java env.
	CallStatic:: env CallStatic.
	generator inflate: CallStatic.
	(*generator clean: CallStatic*)
)
) : (
)
class JNILibraryStartVM classpath: classpath = Test classpath: classpath (
(* Instantiate VM Instance and run mainClass in classPath. *)|
	jvm
	env
|) (
createVM = (
	| options initArgs jvmPtrBuffer envPtrBuffer result success |

	options:: jni JavaVMOption newC.
	options optionString: (('-Djava.class.path=', classpath) asAlien address).
	
	initArgs:: jni JavaVMInitArgs newC.
	initArgs version: (jni VERSION_1_2).
	initArgs nOptions: 1.
	initArgs options: (options address).
	initArgs ignoreUnrecognized = (jni FALSE).

	(* 	envPtr is equivalent to JNIEnv* env;
		*env (i.e. the data pointed to by envPtr is itself the address of the JNINativeInterface struct	*)
	jvmPtrBuffer:: Alien newC: 4.
	envPtrBuffer:: Alien newC: 4.

	result:: jni CreateJavaVM: (jvmPtrBuffer address) p_env: (envPtrBuffer address) vm_args: (initArgs address).

	result = (jni OK)
	ifTrue: [
		jvm:: jni JavaVM withVmPtr: (derefPtrPtr: jvmPtrBuffer).
		env:: jni JNIEnv withEnvPtr: (derefPtrPtr: envPtrBuffer).
	].

	^result
)
createVMAndIfAlreadyExistsTry: block = (
	| result |
	result:: createVM.
	result = (jni EEXIST) ifTrue: [result:: block value].

	^result = (jni OK)
)
derefPtrPtr: pointerPointerAlien = (
	^jni derefPtrPtr: pointerPointerAlien
)
destroyVM = (
	^jvm DestroyJavaVM
)
findClass: name = (
	| javaClass |
	javaClass:: env FindClass: (name asAlien address).
	^(javaClass address = 0) ifFalse: [javaClass] ifTrue: [nil].
)
getMainMethodID: javaClass = (
	| sig mainMethod |
	sig:: '([Ljava/lang/String;)V' asAlien.
	mainMethod:: env GetStaticMethodID: javaClass name: ('main' asAlien address) sig: (sig address).
	^(mainMethod address = 0) ifFalse: [mainMethod] ifTrue: [nil].
)
loadEnvFromVM: jvmInst = (
	| envPtrBuffer result |
	envPtrBuffer:: Alien newC: 4.
	result:: Alien new: 4.

	result:: jvmInst GetEnv: (envPtrBuffer address) version: (jni VERSION_1_2).
	result = (jni OK) ifTrue: [env:: jni JNIEnv withEnvPtr: (derefPtrPtr: envPtrBuffer)].
	^result
)
loadExistingVM = (
	| jvmPtrBuffer jvmInst nVMsBuffer numVMs result |
	jvmPtrBuffer:: Alien newC: 4.
	nVMsBuffer:: Alien newC: 4.
	
	result:: jni GetCreatedJavaVMs: (jvmPtrBuffer address) bufLen: 1 nVMs: (nVMsBuffer address).
	result = (jni OK) ifFalse: [^result].

	numVMs:: nVMsBuffer unsignedLongAt: 1.
	numVMs = 1 ifFalse: [^jni ERR]. (* Would always be 0 or 1 in modern JNI *)

	jvmInst:: jni JavaVM withVmPtr: (derefPtrPtr: jvmPtrBuffer).
	result:: loadEnvFromVM: jvmInst.
	result = (jni OK) ifTrue: [jvm:: jvmInst].
	
	^result
)
makeEmptyArrayOf: javaClass = (
	| emptyArray |
	emptyArray:: env NewObjectArray: 0 elementClass: javaClass initialElement: nullPtr.
	^(emptyArray address = 0) ifFalse: [emptyArray] ifTrue: [nil]
)
nullPtr = (
	^Alien forPointer: 0
)
openFreshVM = (
	(* 	THIS MAY NEVER WORK
		JDK-4712793 : JNI : Failure in JNI_CreateJavaVM() after calling DestroyJavaVM()
		http://bugs.java.com/bugdatabase/view_bug.do?bug_id=4712793	*)

	^createVMAndIfAlreadyExistsTry: [recreateVM]
)
openVM = (
	^createVMAndIfAlreadyExistsTry: [loadExistingVM]
)
recreateVM = (
	| result |

	result:: loadExistingVM.
	result = (jni OK) ifFalse: [^result].

	result:: destroyVM.
	result = (jni OK) ifFalse: [^result].

	^createVM
)
public run = (
	(*openFreshVM ifTrue: [*)
	openVM ifTrue: [
		runMainClass ifFalse: [
			couldNotInstantiateMainClass
		]
	] ifFalse: [
		couldNotOpenVM
	]
)
runMainClass = (
	| javaGraphicsClass javaGraphicsMain stringClass objectArrayArg arrayOfArgs |
	javaGraphicsClass:: findClass: 'JavaGraphics'.
	javaGraphicsClass isNil ifTrue: [^false].

	javaGraphicsMain:: getMainMethodID: javaGraphicsClass.
	javaGraphicsMain isNil ifTrue: [^false].

	stringClass:: findClass: 'java/lang/String'.	
	stringClass isNil ifTrue: [^false].

	objectArrayArg:: makeEmptyArrayOf: stringClass.
	objectArrayArg isNil ifTrue: [^false].

	(* Through CallStaticVoidMethodA method, which takes an array of jvalue arguments...*)

	(* Here a pointer to an array of java arguments *)
	(*arrayOfArgs:: Alien newC: 4.
	arrayOfArgs unsignedLongAt: 1 put: (objectArrayArg address).
	env CallStaticVoidMethodA: javaGraphicsClass methodID: javaGraphicsMain args: (arrayOfArgs address).*)
	
	(* Through CallStaticVoidMethod method, which accepts a variable number of jvalue arguments... *)
	
	(* Here just sending one argument *)
	env CallStaticVoidMethod: javaGraphicsClass methodID: javaGraphicsMain with: objectArrayArg.
	
	(* Here sending variable number of arguments *)
	(*env CallStaticVoidMethod: javaGraphicsClass methodID: javaGraphicsMain withArguments: {objectArrayArg}.*)

	^true.
)
) : (
)
class JavaAliensCallStaticMethods classpath: classpath = Test classpath: classpath (|
|) (
public run = (
	| javaClass javaObject javaBoolean |
	java loadClasspath: classpath.

	javaClass:: java JavaClass find: 'JavaGraphics'.
	javaObject:: javaClass call: 'builder' sig: '()LJavaGraphics;'.
	javaBoolean:: javaClass call: 'getBooleanValue' sig: '()Z'.

	javaObject inspect.
	javaBoolean inspect.
)
) : (
)
class JavaAliensRunApplication classpath: classpath = Test classpath: classpath (|
|) (
public run = (
	java loadClasspath: classpath.
	java runApplication: 'JavaGraphics' args: {'Java App Started By Newspeak'}.
)
) : (
)
class Test classpath: classpath = (|
	public classpath = classpath.
|run) (
run = (
	subclassResponsibility.
)
) : (
)
public main: platform args: args = (
	jni:: JNILibrary usingPlatform: platform.
	java:: JavaAliens usingPlatform: platform usingJNILibrary: jni.
	regex:: RegexMatcher usingPlatform: platform.
	generator:: Generator usingPlatform: platform usingRegex: regex.
	
	List:: platform collections List.
	Alien:: platform aliens Alien.

	runTests
)
runTests = (
	| tests classpath |
	classpath:: '.\bin'.
	(*tests:: allTests.*)
	tests:: {JNILibraryGenerate}.
	tests do: [:eachTest | eachTest classpath: classpath].
)
) : (
)
